;   Copyright (c) Cognitect, Inc. All rights reserved.
;   The use and distribution terms for this software are covered by the
;   Eclipse Public License 1.0 (http://opensource.org/licenses/eclipse-1.0.php)
;   which can be found in the file epl-v10.html at the root of this distribution.
;   By using this software in any fashion, you are agreeing to be bound by
;   the terms of this license.
;   You must not remove this notice, or any other, from this software.

(require '[datomic.client.api :as d]
         '[clojure.data.generators :as gen])
(import '(java.util UUID))

(def client (d/client {:server-type :dev-local :system "day-of-datomic-cloud"}))
(d/create-database client {:db-name "social-news"})
(def conn (d/connect client {:db-name "social-news"}))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; schema

(def schema
  [;; stories
   {:db/ident :story/title
    :db/valueType :db.type/string
    :db/cardinality :db.cardinality/one}
   {:db/ident :story/url
    :db/valueType :db.type/string
    :db/cardinality :db.cardinality/one
    :db/unique :db.unique/identity}
   {:db/ident :story/slug
    :db/valueType :db.type/string
    :db/cardinality :db.cardinality/one}

   ;; comments
   {:db/ident :news/comments
    :db/valueType :db.type/ref
    :db/cardinality :db.cardinality/many
    :db/isComponent true}
   {:db/ident :comment/body
    :db/valueType :db.type/string
    :db/cardinality :db.cardinality/one}
   {:db/ident :comment/author
    :db/valueType :db.type/ref
    :db/cardinality :db.cardinality/one}

   ;; users
   {:db/ident :user/firstName
    :db/valueType :db.type/string
    :db/cardinality :db.cardinality/one}
   {:db/ident :user/lastName
    :db/valueType :db.type/string
    :db/cardinality :db.cardinality/one}
   {:db/ident :user/email
    :db/unique :db.unique/identity
    :db/valueType :db.type/string
    :db/cardinality :db.cardinality/one}
   {:db/ident :user/passwordHash
    :db/valueType :db.type/string
    :db/cardinality :db.cardinality/one}
   {:db/ident :user/upVotes
    :db/valueType :db.type/ref
    :db/cardinality :db.cardinality/many}

   ;; publish time
   {:db/ident :publish/at
    :db/valueType :db.type/instant
    :db/cardinality :db.cardinality/one}])

(d/transact conn {:tx-data schema})

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; data

(def data
  [;; stories
   {:story/title "Teach Yourself Programming in Ten Years"
    :story/url "http://norvig.com/21-days.html"}
   {:story/title "Clojure Rationale"
    :story/url "http://clojure.org/rationale"}
   {:story/title "Beating the Averages"
    :story/url "http://www.paulgraham.com/avg.html"}

   ;; users
   {:user/firstName "Stu"
    :user/lastName "Halloway"
    :user/email "stuarthalloway@datomic.com"}
   {:user/firstName "Ed"
    :user/lastName "Itor"
    :user/email "editor@example.com"}])

(d/transact conn {:tx-data data})

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; point in time db value
(def db (d/db conn))

(def all-stories
  (-> (d/q '[:find ?e
             :where [?e :story/url]]
           (d/db conn))
      flatten
      vec))

(def new-user-id "user-1")

(def upvote-all-stories
  "Transaction data for new-user-id to upvote all stories"
  (mapv
    (fn [story] [:db/add new-user-id :user/upVotes story])
    all-stories))

(def new-user
  "Transaction data for a new user"
  [{:db/id new-user-id
    :user/email "john@example.com"
    :user/firstName "John"
    :user/lastName "Doe"}])

(def upvote-tx-result
  "In a single transaction, create new user and upvote all stories"
  (->> (concat upvote-all-stories new-user)
       (hash-map :tx-data)
       (d/transact conn)))

(def change-user-name-result
  "Demonstrates upsert. Tempid will resolve to existing id to
   match specified :user/email."
  (d/transact
    conn
    {:tx-data
     [{:user/email "john@example.com" ;; this finds the existing entity
       :user/firstName "Johnathan"}]}))

(def john [:user/email "john@example.com"])

(def johns-upvote-for-pg
  (ffirst
    (d/q '[:find ?story
           :in $ ?e
           :where [?e :user/upVotes ?story]
           [?story :story/url "http://www.paulgraham.com/avg.html"]]
         (d/db conn)
         john)))

(def db (:db-after (d/transact
                     conn
                     {:tx-data [[:db/retract john :user/upVotes johns-upvote-for-pg]]})))

;; should now be only two, since one was retracted
(d/pull db '[:user/upVotes] john)

(def data-that-retracts-johns-upvotes
  (let [db (d/db conn)]
    (->> (d/q '[:find ?op ?e ?a ?v
                :in $ ?op ?e ?a
                :where [?e ?a ?v]]
              db
              :db/retract
              john
              :user/upVotes)
         (into []))))

(def db (:db-after (d/transact conn {:tx-data data-that-retracts-johns-upvotes})))

;; all gone
(d/pull db '[:user/upVotes] john)

(defn choose-some
  "Pick zero or more items at random from a collection"
  [coll]
  (take (gen/uniform 0 (count coll))
        (gen/shuffle coll)))

(defn gen-users-with-upvotes
  "Make transaction data for example users, possibly with upvotes"
  [stories email-prefix n]
  (mapcat
   (fn [n]
     (let [user-id (str "new-user-" (UUID/randomUUID))
           upvotes (map (fn [story] [:db/add user-id :user/upVotes story])
                        (choose-some stories))]
       (conj
        upvotes
        {:db/id user-id
         :user/email (str email-prefix "-" n "@example.com")})))
   (range n)))

(def ten-new-users
  (gen-users-with-upvotes all-stories "user" 10))

(d/transact conn {:tx-data ten-new-users})

;; how many users are there?
(d/q '[:find (count ?e)
       :where [?e :user/email ?v]] (d/db conn))

;; how many users have upvoted something?
(d/q '[:find (count ?e)
       :where [?e :user/email]
       [?e :user/upVotes]]
     (d/db conn))

;; Datomic does not need a left join to keep entities missing
;; some attribute. Just leave that attribute out of the :where,
;; and then pull it during the :find.
(def users-with-upvotes
  (d/q '[:find (pull ?e [:user/email {:user/upVotes [:story/url]}])
         :where [?e :user/email]] (d/db conn)))